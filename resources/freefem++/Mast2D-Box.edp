/**
  * @brief Generates the bounding box for the Mast (2D) optimization example.
  * @code{.unparsed}
  *     000000000000000000
  *     0                0
  *     0                0
  *     0                0
  *     NNNNN        NNNNN
  *         0        0
  *         0        0
  *         0        0
  *         DDDDDDDDDD
  * 0 - Homogenous Neumann
  * N - Inhomogenous Neumann
  * D - Homogenous Dirichlet
  * @endcode
  */
include "RodinFF++.idp"

// Boundary attributes
int Gamma0 = 1, GammaD = 2, GammaN = 3, GammaNA = 5;

// Mesh generation
border bot1(t=0.0,0.4){x=-0.5+t; y=0.0;   label=GammaD;};
border bot(t=0.4,0.6){x=-0.5+t; y=0.0;    label=Gamma0;};
border bot2(t=0.6,1.0){x=-0.5+t; y=0.0;   label=GammaD;};
border right1(t=0.0,2.0){x=0.5; y=t;      label=Gamma0;};
border botr1(t=0.0,0.1){x=0.5+t; y=2.0;   label=Gamma0;};
border botr2(t=0.1,0.4){x=0.5+t; y=2.0;   label=GammaN;};
border botr3(t=0.4,0.5){x=0.5+t; y=2.0;   label=GammaNA;};
border right2(t=0.0,1.0){x=1.0; y=2.0+t;  label=Gamma0;};
border top(t=0.0,2.0){x=1.0-t; y=3.0;     label=Gamma0;};
border left2(t=0.0,1.0){x=-1.0; y=3.0-t;  label=Gamma0;};
border botl3(t=0.0,0.1){x=-1.0+t; y=2.0;  label=GammaNA;};
border botl2(t=0.1,0.4){x=-1.0+t; y=2.0;  label=GammaN;};
border botl1(t=0.4,0.5){x=-1.0+t; y=2.0;  label=Gamma0;};
border left1(t=0.0,2.0){x=-0.5; y=2.0-t;  label=Gamma0;};
mesh Th = buildmesh(
    bot1(10) + bot(30) + bot2(10)
    + right1(100) + botr1(5) + botr2(20) + botr3(5) + right2(50)
    + top(100)
    + left2(50) + botl3(5) + botl2(20) + botl1(5) + left1(100));

// Holes
real[int] cx = [
    0.0, -0.2, -0.2, 0.2, 0.2, 0.0, 0.0, 0.0, 0.0, -0.6, 0.6, -0.6, 0.6];
real[int] cy = [
    0.0, 0.4,  1.2, 0.4, 1.2, 0.8, 1.6, 2.0, 2.6,  2.8, 2.8,  2.4, 2.4];

// Distance
fespace Vh(Th, P1);
Vh phi0, phi;
phi0 = holes2d(cx.n, cx, cy, 0.1);
distance(Th, phi0, phi[]);

// Save
savemesh(Th, "mast2d-box.mesh");
savesol("mast2d-box.sol", Th, phi);
